//Find out MDE.Windows extension where installation status is 'Failure'
AzureActivity
| where CategoryValue == 'Administrative'
| where ResourceProviderValue contains "MICROSOFT.COMPUTE"
| extend ['Scope'] = tostring(parse_json(Authorization).scope)
| extend ['ErrorMessage'] = tostring(parse_json(tostring(parse_json(tostring(parse_json(tostring(parse_json(Properties).statusMessage)).error)).details))[0].message)
| extend ['Error Code'] = tostring(parse_json(tostring(parse_json(tostring(parse_json(Properties).statusMessage)).error)).code)
| extend ['ResourceName'] = tostring(Properties_d.resource)
| extend ['Extension'] = split(['ResourceName'],'/')[0]
| extend ['VM'] = split(['ResourceName'],'/')[1]
| where ErrorMessage contains "Failed to configure Microsoft Defender for Endpoint: Onboarding to MDE via Microsoft Defender for Cloud"
| where ActivityStatusValue contains "Failure"
| where Scope contains "MDE.Windows"
| project TimeGenerated, SubscriptionId, ResourceGroup, ActivityStatus, CallerIpAddress, Caller, ['ErrorMessage'], ['Error Code'], ['VM'], ['Extension']

//Find out MDE.Windows extension where installation status is 'Success'
AzureActivity
| where CategoryValue == 'Administrative'
| where ResourceProviderValue contains "MICROSOFT.COMPUTE"
| extend message_ = tostring(parse_json(Properties).message)
| extend ['Scope'] = tostring(parse_json(Authorization).scope)
| extend ['ResourceName'] = tostring(Properties_d.resource)
| extend ['Extension'] = split(['ResourceName'],'/')[0]
| extend ['VM'] = split(['ResourceName'],'/')[1]
| where message_ contains "Microsoft.Compute/virtualMachines/extensions/write"
| where ActivityStatusValue contains "Success"
| where ['Scope'] contains "MDE.Windows"
| project TimeGenerated, SubscriptionId, ResourceGroup, ActivityStatus, CallerIpAddress, Caller, ['VM'], ['Extension']

//Find out Azure Arc connected Windows machines where MDE extension installation status is failed
AzureActivity
| where CategoryValue == 'Administrative'
| where ResourceProviderValue contains "MICROSOFT.HYBRIDCOMPUTE"
| extend ['Scope'] = tostring(parse_json(Authorization).scope)
| extend ['ErrorMessage'] = tostring(parse_json(tostring(parse_json(tostring(parse_json(Properties).statusMessage)).error)).message)
| extend ['Error Code'] = tostring(parse_json(tostring(parse_json(tostring(parse_json(Properties).statusMessage)).error)).code)
| extend ['ResourceName'] = tostring(Properties_d.resource)
| extend ['VMName'] = split(['ResourceName'],'/')[0]
| extend ['Extension'] = split(['ResourceName'],'/')[1]
| where ErrorMessage contains "The resource operation completed with terminal provisioning state 'Failed'"
| where ActivityStatusValue contains "Failure"
| where Scope contains "MDE.Windows"
| project TimeGenerated, ['ResourceName'], ['Resource'], ['ErrorMessage'], ['Error Code'], ['Scope'], SubscriptionId, CallerIpAddress, Caller