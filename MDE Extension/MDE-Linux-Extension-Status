//Find out MDE.Linux extension installations where installation status is success
//Find out MDE.Linux extension success deployments
AzureActivity
| where CategoryValue == 'Administrative'
| where ResourceProviderValue contains "MICROSOFT.COMPUTE"
| extend ['Scope'] = tostring(parse_json(Authorization).scope)
| extend ['Error Code'] = tostring(parse_json(tostring(parse_json(tostring(parse_json(Properties).statusMessage)).error)).code)
| extend ['ResourceName'] = tostring(Properties_d.resource)
| extend ['Extension'] = split(['ResourceName'],'/')[0]
| extend ['VM'] = split(['ResourceName'],'/')[1]
| where ActivityStatusValue contains "Success"
| where ['Scope'] contains "MDE.Linux"
| project TimeGenerated, SubscriptionId, ResourceGroup, ActivityStatus, CallerIpAddress, Caller, ['VM'], ['Extension']


//Find out MDE.Linux extension installations where installation status is Failed
AzureActivity
| where CategoryValue == 'Administrative'
| where ResourceProviderValue contains "MICROSOFT.COMPUTE"
| extend ['Scope'] = tostring(parse_json(Authorization).scope)
| extend ['ErrorMessage'] = tostring(parse_json(tostring(parse_json(tostring(parse_json(tostring(parse_json(Properties).statusMessage)).error)).details))[0].message)
| extend ['Error Code'] = tostring(parse_json(tostring(parse_json(tostring(parse_json(Properties).statusMessage)).error)).code)
| extend ['ResourceName'] = tostring(Properties_d.resource)
| extend ['Extension'] = split(['ResourceName'],'/')[0]
| extend ['VM'] = split(['ResourceName'],'/')[1]
| where ErrorMessage contains "Failed to configure Microsoft Defender for Endpoint: Onboarding to MDE via Microsoft Defender for Cloud"
| where ActivityStatusValue contains "Failure"
| where Scope contains "MDE.Linux"
| project TimeGenerated, SubscriptionId, ResourceGroup, ActivityStatus, CallerIpAddress, Caller, ['ErrorMessage'], ['Error Code'], ['VM'], ['Extension']